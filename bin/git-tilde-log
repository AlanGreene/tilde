#!/bin/bash

# This is a companion script for Tilde's .gitconfig.


set -o pipefail;

git log --pretty=concise -n 20 "$@" | sed '
	# Change "3 weeks ago" into "3 weeks".
	s/^\([^)]*\) ago)/\1)/;

	# Abbreviate the time units.
	s/^\([^)]*\) \(s\)econds)/\1\2)/;
	s/^\([^)]*\) \(m\)inutes)/\1\2)/;
	s/^\([^)]*\) \(h\)ours)/\1\2)/;
	s/^\([^)]*\) \(d\)ays)/\1\2)/;
	s/^\([^)]*\) \(w\)eeks)/\1\2)/;
	s/^\([^)]*\) \(mon\)ths)/\1 \2)/;
	s/^\([^)]*\) \(y\)ears)/\1\2)/;

	# Use at least two digits before the time unit for nicer alignment
	# (until there are commits older than nine years). Of course, because of
	# "mon" for "months", alignment is off for old-ish commits.
	s/(\([0-9]\)\([^0-9 ]\)/ (\1\2/;
';
